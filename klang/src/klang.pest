// Pest grammar for Klang.

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* ~ "\n" }

program = { SOI ~ import_statement? ~ function_def* ~ EOI }

import_statement = { "from" ~ identifier ~ "import" ~ (identifier ~ ("," ~ identifier)*) }

function_def = { "fn" ~ identifier ~ "(" ~ parameter_list? ~ ")" ~ block }
parameter_list = { identifier ~ ("," ~ identifier)* }

block = { "{" ~ statement* ~ "}" }

statement = {
    expression_stmt |
    assignment_stmt |
    for_loop |
    while_loop |
    if_statement |
    parallel_execution
}

expression_stmt = { expression ~ ";" }
assignment_stmt = { identifier ~ "=" ~ expression ~ ";" }

for_loop = { "for" ~ identifier ~ ":" ~ expression ~ block }
while_loop = { "while" ~ expression ~ block }
if_statement = { "if" ~ "(" ~ expression ~ ")" ~ block }

parallel_execution = { "*" ~ "(" ~ expression ~ ("," ~ expression)* ~ ")" ~ ";" }

expression = { atom ~ (infix ~ atom)* }
atom = _{
    function_call |
    literal |
    identifier |
    "(" ~ expression ~ ")"
}

infix = _{ add | subtract | multiply | divide }
add = { "+" }
subtract = { "-" }
multiply = { "*" }
divide = { "/" }

function_call = { identifier ~ "(" ~ (expression ~ ("," ~ expression)*)? ~ ")" }
binary_operation = { expression ~ operator ~ expression }
unary_operation = { operator ~ expression }

literal = { number | string | boolean | array }
number = @{
    ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? ~ unit?
}

unit = @{
    // Length units (metric)
    "mm" | "cm" | "m" | "km" |
    // Length units (imperial)
    "in" | "ft" | "yd" | "mi" |
    // Time units
    "ms" | "s" | "min" | "hr" | "day" |
    // Angle units
    "deg" | "rad"
}

string = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
boolean = { "true" | "false" }
array = { "[" ~ (expression ~ ("," ~ expression)*)? ~ "]" }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
operator = { "+" | "-" | "*" | "/" | "=" | "!" | "?" | ":" }
